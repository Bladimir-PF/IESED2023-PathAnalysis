!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! MPLUS ONLY READS 90 CHARACTERS ACROSS !!!!!!!!!!!!!!!!!!

TITLE:  Mplus Example 6a Multiple-Group Path Model with Indirect Effects
DATA:   FILE = Mindful.csv;       ! Can just list file name if data are in same folder
        FORMAT = free;            ! FREE (default) or FIXED format
        TYPE = individual;        ! Individual (default) or matrix data as input
VARIABLE:
! Names of all variables in data set 
  NAMES = ID SexMW MindC Intern Extern Hostile Benev Warmth;
! Names of all variables in model
  USEVARIABLES = SexMW MindC Intern Extern Hostile Benev Warmth;
! Missing data indicator 
  MISSING = ALL(-999);
! Grouping variable for multiple group analysis
  GROUPING = SexMW (0=Men, 1=Women);

ANALYSIS:   TYPE = GENERAL;          ! For path models
            ESTIMATOR = MLR;         ! Robust ML (cannot use with bootstrapping)
            !BOOTSTRAP IS 1000;      ! Bootstrapping for indirect effects
OUTPUT:     STDYX;                   ! Standardized solution
            !MODINDICES (3.84);      ! Cheat codes to improve model fit (not with CONSTRAINT)
            CINTERVAL;               ! Confidence interval for indirect effects
            !CINTERVAL(BCBOOTSTRAP); ! Bootstrap CI for indirect effects (not with MLR)

! Model code: ON = Y ON X, WITH = covariance (labels to do math on or create constraints)
MODEL:
! Bring X into the likelihood by estimating its mean and variance in empty model
  [MindC] (mXint); MindC (mXvar);
! Intercepts and residual variances for other variables
  [Intern Extern Hostile Benev Warmth] (mM1int mM2int mM3int mM4int mYint);
   Intern Extern Hostile Benev Warmth  (mM1var mM2var mM3var mM4var mYvar);
! Direct MindC --> Warmth
  Warmth ON MindC (mXtoY);
! Left side of model
  Intern Extern Hostile Benev ON MindC (mXtoM1 mXtoM2 mXtoM3 mXtoM4);
! Right side of model
  Warmth ON Intern Extern Hostile Benev (mM1toY mM2toY mM3toY mM4toY);
! All possible residual covariances among mediator variables (not labeled)
  Intern Extern Hostile Benev WITH Intern Extern Hostile Benev;

MODEL Women:
! Bring X into the likelihood by estimating its mean and variance in empty model
  [MindC] (wXint); MindC (wXvar);
! Intercepts and residual variances for other variables
  [Intern Extern Hostile Benev Warmth] (wM1int wM2int wM3int wM4int wYint);
   Intern Extern Hostile Benev Warmth  (wM1var wM2var wM3var wM4var wYvar);
! Direct MindC --> Warmth
  Warmth ON MindC (wXtoY);
! Left side of model
  Intern Extern Hostile Benev ON MindC (wXtoM1 wXtoM2 wXtoM3 wXtoM4);
! Right side of model
  Warmth ON Intern Extern Hostile Benev (wM1toY wM2toY wM3toY wM4toY);
! All possible residual covariances among mediator variables (not labeled)
  Intern Extern Hostile Benev WITH Intern Extern Hostile Benev;

! First list newly created parameters to be defined below 
MODEL CONSTRAINT:
  NEW (mXtoM1Y mXtoM2Y mXtoM3Y mXtoM4Y wXtoM1Y wXtoM2Y wXtoM3Y wXtoM4Y
       mtotXtoY mtotInd wtotXtoY wtotInd 
       dXtoM1 dXtoM2 dXtoM3 dXtoM4 dM1toY dM2toY dM3toY dM4toY
       dXtoM1Y dXtoM2Y dXtoM3Y dXtoM4Y dtotXtoY dtotInd);
! Indirect effects for both groups
  mXtoM1Y = mXtoM1*mM1toY; wXtoM1Y = wXtoM1*wM1toY;
  mXtoM2Y = mXtoM2*mM2toY; wXtoM2Y = wXtoM2*wM2toY;
  mXtoM3Y = mXtoM3*mM3toY; wXtoM3Y = wXtoM3*wM3toY;
  mXtoM4Y = mXtoM4*mM4toY; wXtoM4Y = wXtoM4*wM4toY;
! Total indirect+direct and total indirect effects for both groups
  mtotXtoY = mXtoM1Y + mXtoM2Y + mXtoM3Y + mXtoM4Y + mXtoY;
  mtotInd  = mXtoM1Y + mXtoM2Y + mXtoM3Y + mXtoM4Y;
  wtotXtoY = wXtoM1Y + wXtoM2Y + wXtoM3Y + wXtoM4Y + wXtoY;
  wtotInd  = wXtoM1Y + wXtoM2Y + wXtoM3Y + wXtoM4Y;
! Differences in direct effects across groups
  dXtoM1 = mXtoM1-wXtoM1; dXtoM2 = mXtoM2-wXtoM2;
  dXtoM3 = mXtoM3-wXtoM3; dXtoM4 = mXtoM4-wXtoM4;
  dM1toY = mM1toY-wM1toY; dM2toY = mM2toY-wM2toY;
  dM3toY = mM3toY-wM3toY; dM4toY = mM4toY-wM4toY;
! Differences in indirect effects across groups
  dXtoM1Y = mXtoM1Y-wXtoM1Y; dXtoM2Y = mXtoM2Y-wXtoM2Y;
  dXtoM3Y = mXtoM3Y-wXtoM3Y; dXtoM4Y = mXtoM4Y-wXtoM4Y;
! Differences in total indirect+direct and total indirect effects across groups
  dtotXtoY = mtotXtoY-wtotXtoY; dtotInd = mtotInd-wtotInd; 

! Get all indirect and total effects between Y IND X
MODEL INDIRECT: ! Only available for MVN outcomes
  Warmth IND MindC;

